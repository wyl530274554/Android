apply plugin: 'com.android.application' //引入Gradle插件工具库
apply plugin: 'org.greenrobot.greendao'
//apply plugin: 'io.fabric' //这个加上会报错

repositories {
    maven() {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}
android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    def currentVersionCode = getVersionCode()
    defaultConfig {
        applicationId "com.melon.myapp"
        minSdkVersion 20
        targetSdkVersion 26
        versionCode currentVersionCode
        versionName "1.0"

        flavorDimensions "channel"

        ndk {
            //选择要添加的对应cpu类型的.so库。
//            abiFilters 'armeabi','x86'  'armeabi-v7a', 'armeabi-v8a'
            abiFilters 'armeabi-v7a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }

//        manifestPlaceholders = [
//                JPUSH_PKGNAME: applicationId,
//                JPUSH_APPKEY : "7ce13c9ac6de0788451ece4e", //JPush上注册的包名对应的appkey.
//                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
//        ]
    }

    signingConfigs {
        //系统签名
        common {
            keyAlias 'melon'
            keyPassword '123456'
            storeFile file('myapp.jks')
            storePassword '123456'
        }
    }

    buildTypes {    //构建类型
        release {   //版本配置
            minifyEnabled false
            signingConfig signingConfigs.common
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        Melon {
            dimension "channel"
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    dexOptions {
        preDexLibraries = false //解决问题：DexException - Multiple dex files define
    }

    //包名
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "melon_" + defaultConfig.versionName + "_" + defaultConfig.versionCode + ".apk"
            }
    }

    //DataBinding
    dataBinding {
        enabled = true
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}
/**
 * @param filename 要打开的文件名字
 * @param propName 文件里面的键名字
 * @return 所选择的文件里面的propName对应的值
 */
def getVersionCode() {
    def propsFile = file('version.properties')
    def properties = new Properties()
    properties.load(new FileInputStream(propsFile))
    def vCode = properties['V_CODE'].toInteger()
    properties['V_CODE'] = (++vCode).toString()
    properties.store(propsFile.newWriter(), null)
    return vCode
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':mylibrary')
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'me.yifeiyuan.periscopelayout:library:1.0.0'
    implementation 'com.bigkoo:convenientbanner:2.0.5'
//    implementation 'com.jude:swipebackhelper:3.1.2'
//    implementation 'cn.jiguang.sdk:jpush:3.0.3'
//    implementation 'cn.jiguang.sdk:jcore:1.1.1'
    implementation 'com.j256.ormlite:ormlite-android:5.1'
    implementation 'com.j256.ormlite:ormlite-core:5.1'
    implementation 'com.jcodecraeer:xrecyclerview:1.3.2'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'cn.yipianfengye.android:zxing-library:2.2'
    implementation 'me.dm7.barcodescanner:zbar:1.9.8'
    implementation('com.github.worker8:tourguide:2.0.0-SNAPSHOT@aar') {
        transitive = true
    }
    implementation 'com.zhy:okhttputils:2.6.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation(name: 'sdkdemo-debug', ext: 'aar')
    implementation 'com.github.hotchemi:permissionsdispatcher:3.3.1'
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:3.3.1"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.2'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    testImplementation 'junit:junit:4.12'
}
